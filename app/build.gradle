apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def getGitSha = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short' , 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {

    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.keylesspalace.tusky"
        minSdkVersion 19
        targetSdkVersion 27

        versionCode 49
        versionName "3.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
        }

        debug { }
    }

    flavorDimensions "stage", "store"
    productFlavors {
        /**
         * Flavor used to build a release for the Google Play Store.
         *
         * Contains a preconfigured, non editable blacklist of instances
         * showing content against Google's policies so that the app will
         * not be removed by Google due to a violation of their ToS.
         */
        google {
            dimension "store"
        }

        /**
         * Flavor used to build a release for F-Droid and other stores.
         *
         * Does not contain a blacklist of instances so that all users will
         * be able to freely access the entirety of the web without having
         * to worry about Google's ToS for developers and app publishers.
         */
        unbound {
            dimension "store"
        }

        /**
         * Flavor used for release builds.
         */
        release {
            dimension "stage"
        }

        /**
         * Flavor used for development builds.
         *
         * Optimizes build speed by implementing the suggestions documented at:
         * https://developer.android.com/studio/build/optimize-your-build
         *
         * WARNING:
         * Feel free to update these specifications to match your device's
         * hardware but DO NOT commit or push your changes to upstream.
         */
        development {
            dimension "stage"

            // Speed up compilation by tighter device targeting
            minSdkVersion 27
            resConfig "en", "xxhdpi", "xxxhdpi"

            // Install the development app alongside release
            applicationIdSuffix ".debug"
            versionNameSuffix "-${getGitSha}"
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    androidExtensions {
        experimental = true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

ext.supportLibraryVersion = '27.1.1'
ext.daggerVersion = '2.17'

/**
 * WARNING:
 * Every time this list is updated, either by adding, removing or updating libraries
 * you MUST check that they are listed along with their license in LicenseActivity.
 */
dependencies {

    // Kotlin Runtime
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Android Support - Core
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:support-v13:$supportLibraryVersion"

    // Android Support - Layouts
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"

    // Android Support - Emotes
    implementation "com.android.support:support-emoji:$supportLibraryVersion"
    implementation "com.android.support:support-emoji-appcompat:$supportLibraryVersion"

    // Android Support - Extras
    implementation "com.android.support:exifinterface:$supportLibraryVersion"
    implementation "com.android.support:customtabs:$supportLibraryVersion"

    // Android Support - Architecture
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    kapt 'android.arch.persistence.room:compiler:1.1.1'

    // Dependency Injection
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // Reactive Architecture
    implementation 'io.reactivex.rxjava2:rxjava:2.2.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'com.uber.autodispose:autodispose-android-archcomponents:1.0.0-RC2'
    implementation 'com.uber.autodispose:autodispose-ktx:1.0.0-RC2'

    // Background Jobs
    implementation 'com.evernote:android-job:1.2.6'

    // Networking - HTTP and Sockets
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'

    // Networking - REST APIs
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    // Networking - Images
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    // UI - Layouts
    implementation('com.mikepenz:materialdrawer:6.0.9@aar') {
        transitive = true
    }
    implementation('com.theartofdev.edmodo:android-image-cropper:2.7.0') {
        exclude group: 'com.android.support'
    }

    // UI - Widgets
    implementation 'com.github.chrisbanes:PhotoView:2.1.4'
    implementation 'com.github.connyduck:sparkbutton:1.0.1'

    // UI - Resources
    implementation 'com.mikepenz:google-material-typeface:3.0.1.2.original@aar'
    implementation 'de.c1710:filemojicompat:1.0.14'

    // Debug Utilities
    debugImplementation 'im.dino:dbinspector:3.4.1@aar'

    // Testing - Mocking
    testImplementation 'org.mockito:mockito-inline:2.21.0'

    // Testing - Local JVM
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.8'

    // Testing - Android Specific
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
